def versionMajor = 0
def versionMinor = 1
def versionPatch = 1
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.18.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "$project.name-$variant.name-$variant.versionName-${variant.versionCode}.apk")
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    defaultConfig {
        applicationId "cz.droidboy.worktime"
        minSdkVersion 15
        targetSdkVersion 22

        resConfigs "cs", "sk", "en"
        resConfigs "nodpi", "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix '.debug'
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'

    compile 'com.github.PhilJay:MPAndroidChart:v2.1.0'
    compile 'net.danlew:android.joda:2.7.2'
    compile 'com.jakewharton.timber:timber:3.0.2'

    debugCompile 'com.facebook.stetho:stetho:1.1.1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    compile "com.crashlytics.sdk.android:crashlytics:2.2.4"
}
